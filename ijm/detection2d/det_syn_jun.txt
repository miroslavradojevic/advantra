// detects critical points in generated synthetic junctions
// folder structure & nomenclature has to be consistent
// assumes that parameters used at particular stage were embedded in the folder name
// mechanism to extract generation parameters and infer the detection parameters

k = 2; // when determining D for detection wrt. Dmax
s = 1.1;
L = 10;
ncc_high=0.95;
ncc_low=0.1;
likelihood_high=0.5;
likelihood_low=0;
out=0.4; // this is really out_sigma

dir_name = getArgument();
if (dir_name=="") exit ("script needs input folder path");
if (!File.exists(dir_name)) exit (dir_name + " does not exist");
if (!endsWith(dir_name, "/")) dir_name+="/";

folder_prefix = "synjun"; // expect folders with such prefix in

// loop directories and do the standardized detection take k*Dmax as argument

print("scanning... " + dir_name);

list = getFileList(dir_name);

for (i=0; i<list.length; i++) {  // loop folders with configurations

	if (endsWith(list[i], "/") && startsWith(list[i], folder_prefix)) {

		//print(dir_name+list[i]);
		sub_list = getFileList(dir_name+list[i]);
        
        for (j=0; j<sub_list.length; j++) { // loop files in each folder

            // list[]       contains generation parameters
            // sub_list[]   contains file names

			if (endsWith(dir_name+list[i]+sub_list[j], ".tif")) {

				// set detection parameters
				components = split(list[i], "_");
				Dmax        = parseFloat(components[1]);
				Ddet        = k   * Dmax;


				run("Critpoint2D",
				"select="+dir_name+list[i]+sub_list[j]+" "+
				"bifurcations "+
				"endpoints "+ // no interactive for batch execution
				"s="+toString(s)+" "+
				"dlist="+toString(Ddet)+" "+
				"l="+toString(L)+" "+
				"ncc_high="+toString(ncc_high)+" "+
				"ncc_low="+toString(ncc_low)+" "+
				"likelihood_high="+toString(likelihood_high)+" "+
				"likelihood_low="+toString(likelihood_low)+" "+
				"out_sig="+toString(out)+"");


                print(dir_name+list[i]+sub_list[j]);

				//close();
                //run("Close All");
				
			}
        
        }
        
    }
     
}

print("DONE");